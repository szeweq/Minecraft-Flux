buildscript {
	repositories {
		jcenter()
		maven {url "http://files.minecraftforge.net/maven"}
		maven {url "https://plugins.gradle.org/m2/"}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"

sourceCompatibility = targetCompatibility = 1.8

idea { module { inheritOutputDirs = true } }

String[] versionsMC = ["1.11", "1.11.2"]
String versionForge = "13.19.1.2189"

task deobfJar(type: Jar, dependsOn: "reobfJar") {
	from sourceSets.main.output
	from sourceSets.api.output
	classifier = "dev"
}
task sourcesJar(type: Jar, dependsOn: "sourceJar") {
	from sourceSets.main.allJava
	from sourceSets.api.allJava
	classifier = "sources"
}
String ttag = System.getenv("TRAVIS_TAG")
version = ttag ? ttag : "0.0.0-x"
group = "szewek.mcflux"
archivesBaseName = "mcflux"

minecraft {
	version = versionsMC[0] + "-" + versionForge
	runDir = "run"
	mappings = "snapshot_20161120"
	replace "@VERSION@", project.version
	replaceIn "R.java"
}

publishing {
	publications {
		MCFluxPublish(MavenPublication) {
			from components.java
			artifact deobfJar
			artifact sourcesJar
			groupId project.group
			artifactId project.archivesBaseName
			version project.version
		}
	}
}

bintray {
	user = System.getenv("BINTRAY_USER")
	key = System.getenv("BINTRAY_KEY")
	publications = ["MCFluxPublish"]
	publish = true
	override = false
	pkg {
		repo = "MC"
		name = project.group
		version {
			name = project.version
		}
	}
}

repositories {
	maven {url "http://dvs1.progwml6.com/files/maven"}
	//maven {url "http://mobiusstrip.eu/maven"}
	maven {url "http://maven.ic2.player.to"}
	//maven {url "http://maven.epoxide.xyz"}
	maven {url "http://maven.tterrag.com"}
}

dependencies {
	deobfCompile "mcjty.theoneprobe:TheOneProbe:1.1x-1.4.0-50"
	deobfCompile "mezz.jei:jei_1.11:4.0.1.193"
	//deobfCompile "mcp.mobius.waila:Waila:1.7.0-B3_1.9.4"
	deobfCompile "net.industrial-craft:industrialcraft-2:2.6.54-ex110:api"
	//deobfCompile "net.darkhax.tesla:Tesla:1.10.2-1.2.1.49"
	runtime fileTree(dir: "libs", include: "*.jar")
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand "version":project.version, "mcversion":project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}
curseforge {
	String cfkey = System.getenv("CURSEFORGE_KEY")
	apiKey = cfkey ? cfkey : ""
	project {
		id = "248942"
		releaseType = "release"
		changelogType = "markdown"
		changelog = file("CHANGELOG.md")
		versionsMC.each {
			addGameVersion it
		}
	}
}
artifacts {
	archives deobfJar
	archives sourcesJar
}